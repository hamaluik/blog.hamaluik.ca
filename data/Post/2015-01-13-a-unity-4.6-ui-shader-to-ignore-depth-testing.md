---
title: A Unity 4.6 UI Shader to Ignore Depth Testing
slug: a-unity-4.6-ui-shader-to-ignore-depth-testing
author: kenton
published: 2015-01-13 18:16:11
category: gamedev
tags: Unity, Shaders
preview_summary: I’ve started using Unity again for some 3D development with Google Cardboard and have been using the new Unity 4.6 UI (which I think is great by the way). Specifically, in order to get UIs rendered for the user in the 3D environment, I needed to place text on a canvas set to World Space (parenting the canvas to the camera, so that it tracks with the user’s head motion). This places the UI in a physical space, which works well for the 3D view but doesn’t work well for a HUD where you need it to always be displayed over everything.
---

I've started using Unity again for some 3D development with [Google Cardboard](https://www.google.com/get/cardboard/) and have been using the new Unity 4.6 UI (which I think is great by the way). Specifically, in order to get UIs rendered for the user in the 3D environment, I needed to place text on a canvas set to `World Space` (parenting the canvas to the camera, so that it tracks with the user's head motion). This places the UI in a physical space, which works well for the 3D view but doesn't work well for a HUD where you need it to always be displayed over everything.

After some searching around and not finding a fix, I ended up modifying the default Unity UI shader to ignore z-testing; I've shared it here in case someone else runs into the same issue!

Here is the modified shader. Save it as a `.shader` file in your `Assets` folder, then create a material which uses this shader. Apply the material to any and all UI elements you have that you want to ignore the z-testing on.

Happy developing!

```shader
Shader "UI/Unlit/AlwaysOnTop"
{
    Properties
    {
        _Color ("Main Color", Color) = (1,1,1,1)
        _MainTex ("Alpha (A)", 2D) = "white" {}
        
        _StencilComp ("Stencil Comparison", Float) = 8
        _Stencil ("Stencil ID", Float) = 0
        _StencilOp ("Stencil Operation", Float) = 0
        _StencilWriteMask ("Stencil Write Mask", Float) = 255
        _StencilReadMask ("Stencil Read Mask", Float) = 255

        _ColorMask ("Color Mask", Float) = 15
    }

    SubShader
    {
        LOD 200

        Tags
        {
            "Queue" = "Transparent"
            "IgnoreProjector" = "True"
            "RenderType" = "Transparent"
            "PreviewType"="Plane"
        }

        Cull Off
        Lighting Off
        ZWrite Off
        ZTest Always
        Offset -1, -1
        Fog { Mode Off }
        Blend SrcAlpha OneMinusSrcAlpha
        ColorMask [_ColorMask]

        Pass
        {
            CGPROGRAM
                #pragma vertex vert
                #pragma fragment frag
                #include "UnityCG.cginc"

                struct appdata_t
                {
                    float4 vertex : POSITION;
                    half4 color : COLOR;
                    float2 texcoord : TEXCOORD0;
                };

                struct v2f
                {
                    float4 vertex : POSITION;
                    half4 color : COLOR;
                    float2 texcoord : TEXCOORD0;
                };

                sampler2D _MainTex;
                float4 _MainTex_ST;
                fixed4 _Color;

                v2f vert (appdata_t v)
                {
                    v2f o;
                    o.vertex = mul(UNITY_MATRIX_MVP, v.vertex);
                    o.texcoord = TRANSFORM_TEX(v.texcoord, _MainTex);
                    o.color = v.color;
#ifdef UNITY_HALF_TEXEL_OFFSET
                    o.vertex.xy += (_ScreenParams.zw-1.0)*float2(-1,1);
#endif
                    return o;
                }

                half4 frag (v2f i) : COLOR
                {
                    half4 col = i.color;
                    col.a *= tex2D(_MainTex, i.texcoord).a;
                    col = col * _Color;
                    clip (col.a - 0.01);
                    return col;
                }
            ENDCG
        }
    }
}
```